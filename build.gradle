plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.5.20"
    id "org.jetbrains.kotlin.plugin.serialization" version "1.5.20"
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

group = "me.senseiju"
version = "1.0.0"

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://repo.codemc.org/repository/maven-public/" }
    maven { url "https://papermc.io/repo/repository/maven-public/" }
    maven { url "https://repo.citizensnpcs.co/" }
    maven { url "https://jitpack.io" }
    maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url "https://repo.dmulloy2.net/nexus/repository/public/" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0"
    implementation "com.zaxxer:HikariCP:3.4.5"
    implementation "me.mattstudios.utils:matt-framework-gui:2.0.2"
    implementation "me.mattstudios.utils:matt-framework:1.4.6"
    implementation "de.tr7zw:item-nbt-api:2.8.0"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1"
    implementation "com.github.SenseiJu:Sentils:5cb3d9b3fd"

    compileOnly "com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT"
    compileOnly "net.citizensnpcs:citizens:2.0.27-SNAPSHOT"
    compileOnly "net.luckperms:api:5.2"
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    compileOnly "me.clip:placeholderapi:2.10.9"
    compileOnly "com.comphenix.protocol:ProtocolLib:4.5.0"

    testImplementation "com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

shadowJar {
    archiveFileName.set("SennetMC.jar")

    relocate "me.mattstudios.mfgui", "me.senseiju.sennetmc.shaded.mfgui"
    relocate "me.mattstudios.mf", "me.senseiju.sennetmc.shaded.mf"
    relocate "de.tr7zw.changeme.nbtapi", "me.senseiju.sennetmc.shaded.nbtapi"

    minimize()
}

task copyJarToServer(type: Copy) {
    from shadowJar
    into "D:/Servers/Minecraft/SennetMC/plugins/update"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        setDuplicatesStrategy(DuplicatesStrategy.INCLUDE)

        include ("plugin.yml")
        expand("version": project.version)
    }
}

test {
    useJUnitPlatform()
}